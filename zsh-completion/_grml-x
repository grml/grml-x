#compdef grml-x
# Filename:      /usr/share/grml/zsh/completion/grml/_grml-x
# Purpose:       completion for command grml-x for zsh (z shell)
# Authors:       grml-team (grml.org), (c) Michael Prokop <mika@grml.org>
# Bug-Reports:   see http://grml.org/bugs/
# License:       This file is licensed under the GPL v2.
################################################################################

local arguments wm
local -a wms

wms=()

for wm in awesome blackbox dwm evilwm fluxbox fvwm fvwm2 fvwm-crystal \
          gnome-session gnomesession i3 icewm ion2 ion3 jwm larswm    \
	  openbox particleman pekwm pwm3 ratpoison startkde startlxde \
	  twm w9wm windowlab wmi wmii wm-ng xfce4 xfce4-session xfwm4 \
	  xmonad x-window-manager ; do
    [[ -x /usr/bin/$wm ]] && wms+=$wm
done

arguments=(
  '--display[display for xserver]:display:(7 8)'
  '--force[force (re-)creation of xorg.conf file]'
  '--help[display help]:'
  '--hsync[horizontal sync frequencies (<number>)]:hsync frequencies:(28 `seq 30 5 95` 96)'
  '--mode[resolution-mode for xserver (<width>x<height> - e.g. 1024x768)]:resolutions:(1920x1440 1600x1200 1400x1050 1280x1024 1280x960 1024x768 800x600 640x480)'
  '--module[module for xserver]:modules:(`cd /usr/lib/xorg/modules/drivers/ && /bin/ls *.so | sed -e 's/_drv.so//' -e 's/.so//'`)'
  '--nostart[do not start X server]'
  '--vsync[vertical sync frequencies (<number>)]:vsync frequencies:(43 `seq 45 5 70` 72)'
  ':window manager:($wms)'
)
_arguments -s $arguments

## END OF FILE #################################################################
# vim:foldmethod=marker
